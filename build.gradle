plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.sparrowwallet'
version = '1.0'

def secrets = new Properties()
file("publish.properties").withInputStream {
    stream -> secrets.load(stream)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('com.fasterxml.jackson.core:jackson-databind:2.17.2')
    implementation('org.eclipse.jetty:jetty-client:9.4.54.v20240208')
    implementation('io.reactivex.rxjava2:rxjava:2.2.15')
    implementation('org.apache.commons:commons-lang3:3.7')
    implementation('com.google.guava:guava:33.0.0-jre')
    implementation('org.slf4j:slf4j-api:1.7.30')
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "Forgejo-SparrowWallet"
            url = uri("https://code.sparrowwallet.com/api/packages/sparrowwallet/maven")

            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "token ${secrets.token}"
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = 'tern'

            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id = 'craigraw'
                    name = 'Craig Raw'
                    email = 'mail@sparrowwallet.com'
                }
            }
            scm {
                connection = 'scm:git:https://github.com/sparrowwallet/tern.git'
                developerConnection = 'scm:git:git@github.com:sparrowwallet/tern.git'
                url = 'https://github.com/sparrowwallet/tern'
            }
        }
    }
}